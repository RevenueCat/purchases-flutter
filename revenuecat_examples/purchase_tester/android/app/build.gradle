plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace 'com.revenuecat.purchases_sample'
    compileSdk 35
    ndkVersion "27.0.12077973"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lint {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "com.revenuecat.purchases_sample"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // purchases-hybrid-common has two variants, but this library doesn't have any
        // Gradle doesn't know which variant of purchases-hybrid-common to use when using
        // includeBuild and working with a local copy of purchases-hybrid-common
        // This line also needs to be added to purchases_flutter's build.gradle
        // https://developer.android.com/studio/build/build-variants#resolve_matching_errors
        missingDimensionStrategy 'apis', 'defaults'
        multiDexEnabled true
    }

    buildTypes {
        release {
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation 'com.android.support:multidex:1.0.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    // Versions should match Flutter's integration_test package or there will be dependency resolution conflicts
    // https://github.com/flutter/flutter/blob/master/packages/integration_test/android/build.gradle#L52-L55
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
}

// Usage:
// ./gradlew enableLocalBuild -PcommonPath="$HOME/Development/repos/purchases-hybrid-common/android"
// ./gradlew enableLocalBuild -PandroidPath="$HOME/Development/repos/purchases-android"
task enableLocalBuild {
    group = 'Tools'
    description = 'Enable composite build for local dependencies'
    doLast {
        if (project.hasProperty('commonPath')) {
            new File(rootProject.projectDir, ".composite-enable").text = commonPath
            println "Enabled local purchases-hybrid-common at: $commonPath"
        }
        if (project.hasProperty('androidPath')) {
            new File(rootProject.projectDir, ".android-enable").text = androidPath
            println "Enabled local purchases-android at: $androidPath"
        }
        if (!project.hasProperty('commonPath') && !project.hasProperty('androidPath')) {
            throw new GradleException("Please specify -PcommonPath or -PandroidPath parameter")
        }
    }
}

task disableLocalBuild {
    group = 'Tools'
    description = 'Disable composite build'
    doLast {
        def disabled = false
        File commonFile = new File(rootProject.projectDir, ".composite-enable")
        if (commonFile.exists()) {
            commonFile.delete()
            println "Disabled local purchases-hybrid-common"
            disabled = true
        }
        File androidFile = new File(rootProject.projectDir, ".android-enable")
        if (androidFile.exists()) {
            androidFile.delete()
            println "Disabled local purchases-android"
            disabled = true
        }
        if (!disabled) {
            println "No local builds were enabled"
        }
    }
}
