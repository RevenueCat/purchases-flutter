group 'com.revenuecat.purchases_flutter'
version '5.6.5'

buildscript {
    ext.kotlin_version = '1.7.21'
    ext.common_version = '6.1.2'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 31

    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'com.revenuecat.purchases_flutter'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // purchases-hybrid-common has two variants, but this library doesn't have any
        // Gradle doesn't know which variant of purchases-hybrid-common to use when using
        // includeBuild and working with a local copy of purchases-hybrid-common
        // https://developer.android.com/studio/build/build-variants#resolve_matching_errors
        missingDimensionStrategy 'dependencyVersions', 'latestDependencies', 'unityIAP'
    }

    lintOptions {
        disable 'InvalidPackage'
    }
    buildTypes {
        release {
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.revenuecat.purchases:purchases-hybrid-common:$common_version"
}
